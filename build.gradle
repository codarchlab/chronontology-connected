group 'de.dainst.idai'
version '0.1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'commons-codec:commons-codec:1.5'
    compile 'org.skyscreamer:jsonassert:1.2.3'
    compile 'log4j:log4j:1.2.17'
    compile 'com.fasterxml.jackson.core:jackson-core:2.6.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
    compile group: 'com.sparkjava', name: 'spark-core', version: '2.3'
    testCompile 'org.testng:testng:6.9.9'
    testCompile 'org.mockito:mockito-all:1.8.4'
    compile 'org.elasticsearch:elasticsearch:1.7.3'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
}

//create a single Jar with all dependencies
jar {
    doFirst {
        def serviceDir = file("$buildDir/META-INF/services")
        serviceDir.deleteDir()
        serviceDir.mkdirs()

        // copy all service files from deps to buildDir
        for(file in configurations.runtime) {
            zipTree(file).matching{ include 'META-INF/services/*' }.each { f ->
                new File(serviceDir, f.name) << f.getText("UTF-8")
            }
        }
    }

    manifest {
        attributes 'Implementation-Title': project.name,
                'Main-Class': "org.dainst.chronontology.App"
        // etc ...
    }

    from(configurations.runtime.collect{ it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/**' // Don't let Gradle merge service files
    }

    // include service files from the buildDir
    from fileTree(buildDir).matching{ include 'META-INF/services/*' }
}

jar.dependsOn test

test {
    useTestNG()
    scanForTestClasses = false
    include '**/*Test.class'

    testLogging {
        showStandardStreams = true

        // log results to "build/test-results" directory
        exceptionFormat "full"
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}
